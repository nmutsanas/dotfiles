#!/bin/zsh
ZSH_HOME=~/dot/zsh
PROJECTS=~/code
CHEATSHEETS=~/dot/cheatsheets/
# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit

source ${ZSH_HOME}/antigen.bootstrap

# Stash your environment variables in ~/.localrc. This means they'll stay out
# of your main dotfiles repository (which may be public, like this one), but
# you'll have access to them in your scripts.
if [[ -a ~/.localrc ]]
then
  source ~/.localrc
fi

#load the topics

# all of our zsh files
typeset -U config_files # -U to ignore duplicates
config_files=($HOME/dot/**/*.zsh)
yellow "${#config_files} config files found"
# load the path files
local path_cnt=0;
for file in ${(@M)config_files:#*/path.zsh} ; do
	path_cnt=$((path_cnt + 1))
	green $file &&	source $file
done
config_files=(${config_files:#*/path.zsh}) # remove them
# load the env files
local env_cnt=0;
for file in ${(M)config_files:#*/env.zsh}; do
	env_cnt=$((env_cnt + 1))
	green $file && source $file
done
config_files=(${config_files:#*/env.zsh}) # remove them

# load everything else (except completion)
local other_cnt=0;
for file in ${config_files:#*/completion.zsh}; do
	other_cnt=$((other_cnt + 1))
	green $file && source $file
done
config_files=(${(M)config_files:#*/completion.zsh})
# load the completion files
local compl_cnt=0;
for file in ${(M)config_files:#*/completion.zsh}; do
	compl_cnt=$((compl_cnt + 1));
	green $file &&	source $file
done
# print out a report of what was loaded, for debugging
green "${path_cnt} path, ${env_cnt} env, ${other_cnt} other, ${compl_cnt} completion"
